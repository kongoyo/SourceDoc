     A*===============================================================
     A* Window to display or update Customer Master
     A*===============================================================
     A*  CRTDSPF FILE(MTNCUSTD) SRCFILE(...) RSTDSP(*YES)
     A*==============================================================
     A* There is a naming convention for the fields in each record.
     A*  -- Screen Header:   Fields begin with SH_
     A*  -- Details          Fields begin with SD_
     A*  -- Screen footer:   Fields begin with SFT_
     A*===============================================================
     A* Indicator usage:
     A*   01-20 Are not routinely cleared after an EXFMT.  Some
     A*         of these are used in subfile & subfile control for
     A*         highlighting, etc., which does not change unless
     A*         explicitly requested.
     A*   21-99 Are routinely cleared after an EXFMT.
     A*         80-89 Subfile record
     A*         90    Nessage Subfile end
     A*         91-99 Subfile Control Record
     A* Note: INDARA is specified here and in the program and this
     A*       means that the display file indicators are in a
     A*       means that the display file indicators are in a
     A*       data structure separate from the program indicators.
     A*       This makes it easy to name the indicators in the DS.
     a* Note: DSPATR HI and RI at the same time = non-display. Thus
     a*       when we turn on RI were turn of HI,
     a* 01/2021 Lennon Allow lower case on name, address, city
     A*===============================================================
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
     A                                      ALTHELP
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      HELP
     A*===============================================================
     A*=== Screen Header:  Fields begin with SH_ =====================
     A*
     A          R SH_HDR
     A                                      OVERLAY
     A                                      TEXT('Screen Header')
     A                                      WDWBORDER((*COLOR PNK))
     A                                      WINDOW(*DFT 17 54)
     A            SH_PGM        10A  O  1  1
     A                                  1 21'Customer Master'
     A                                  1 47DATE
     A                                      EDTCDE(Y)
     A                                  2  1USER
     A            SH_FUNCT      25A  O  2 16DSPATR(HI)
     A                                  2 47TIME
     A*===============================================================
     A*=== Data Details - Fields begin with SD_ ======================
     A          R DETAILS
     A                                      OVERLAY
     A                                      WINDOW(SH_HDR)
     A                                      CHGINPDFT
     A                                      RTNCSRLOC(&SD_PMT_RCD &SD_PMT_FLD)
     A*---------------------------------------------------------------
     A                                  5  1'Customer Id'
     A            SD_CUSTID      4  0   5 14DSPATR(HI)
     A                                  5 20'Active Status'
     A            SD_ACTIVE      1   B  5 35
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A  10                                  DSPATR(PR)
     A N10                                  DSPATR(UL)
     A                                  6  1'Name'
     A            SD_NAME       40   B  6  9CHECK(LC)
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  10                                  DSPATR(PR)
     A N10                                  DSPATR(UL)
     A                                  7  1'Address'
     A            SD_ADDR       40   B  7  9CHECK(LC)
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A  10                                  DSPATR(PR)
     A N10                                  DSPATR(UL)
     A                                  8  1'City'
     A            SD_CITY       20   B  8  9CHECK(LC)
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A  10                                  DSPATR(PR)
     A N10                                  DSPATR(UL)
     A                                  8 32'ST+'
     A            SD_STATE       2   B  8 36
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A  10                                  DSPATR(PR)
     A N10                                  DSPATR(UL)
     A                                  8 39'ZIP'
     A            SD_ZIP        10   B  8 43
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  10                                  DSPATR(PR)
     A N10                                  DSPATR(UL)
     A                                  9  1'Account Manager'
     A                                 10  5'Phone'
     A            SD_ACCTPH     20   B 10 15
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  10                                  DSPATR(PR)
     A N10                                  DSPATR(UL)
     A                                 11  5'Name'
     A            SD_ACCTMGR    40   B 11 15CHECK(LC)
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A  10                                  DSPATR(PR)
     A N10                                  DSPATR(UL)
     A                                 12  1'Corporate Phone'
     A            SD_CORPPH     20   B 12 17
     A N56                                  DSPATR(HI)
     A  56                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A  10                                  DSPATR(PR)
     A N10                                  DSPATR(UL)
     A                                 13  1'Last Change'
     A N61                                  DSPATR(ND)
     A            SD_CHGTIME    23     13 13
     A N61                                  DSPATR(ND)
     A                                 13 37'by'
     A N61                                  DSPATR(ND)
     A            SD_CHGUSER    15     13 40
     A N61                                  DSPATR(ND)
     A            SD_PMT_RCD    10A  H
     A            SD_PMT_FLD    10A  H
     A*===============================================================
     A*=== Screen footer & function keys:  Fields begin with SFT_ ====
     A*
     A          R SFT_FKEY
     A                                      TEXT('Screen Footer')
     A                                      OVERLAY
     A                                      WINDOW(SH_HDR)
     A                                 14  1'             Demo Corp of America -
     A                                                          '
     A                                      DSPATR(UL)
     A            SFT_KEYS      53A  O 15  1COLOR(BLU)
     A*===============================================================
     A*=== Message Subfile:  No fields ===============================
     A*
     A          R MSGSFL                    SFL
     A                                      TEXT('Message Subfile')
     A                                      SFLMSGRCD(16)
     A            MSGKEY                    SFLMSGKEY
     A            MSGPGMQ                   SFLPGMQ(10)
     A*===============================================================
     A*=== Message Subfile Control: No fields ========================
     A*
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      TEXT('Message Subfile Control')
     A                                      WINDOW(SH_HDR)
     A                                      PAGEDOWN
     A                                      PAGEUP
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N90                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            MSGPGMQ                   SFLPGMQ(10)
     A*===============================================================
     A*=== Dummy Record - Assume =====================================
     A*
     A          R DUMMY
     A                                      ASSUME
     A                                      KEEP
     A                                  1  2'*'
     A*%%RS+<record-sequences>
     A*%%RS+ <sequence name="SCREEN1">
     A*%%RS+  <device type="display" width="80" height="24" />
     A*%%RS+  <record-write record-format="SH_HDR" />
     A*%%RS+  <record-write record-format="DETAILS" />
     A*%%RS+  <record-write record-format="SFT_FKEY" />
     A*%%RS+  <record-write record-format="MSGSFL" />
     A*%%RS+  <record-write record-format="MSGCTL" />
     A*%%RS+  <record-write record-format="DUMMY" />
     A*%%RS+ </sequence>
     A*%%RS </record-sequences>
     A*%%RS+<record-sequences>
     A*%%RS+ <sequence name="Untitled">
     A*%%RS+  <device type="display" width="80" height="24" />
     A*%%RS+ </sequence>
     A*%%RS </record-sequences>
